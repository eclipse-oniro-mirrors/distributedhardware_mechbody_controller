# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/distributedhardware/mechbody_controller/mechbody.gni")

copy_taihe_idl("mech_manager_taihe") {
  sources = [ "idl/ohos.distributedHardware.mechanicManager.taihe" ]
}

config("mech_manager_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "include",
    "$mechbody_controller_path/services/include",
    "$mechbody_controller_path/services/include/controller",
    "$mechbody_controller_path/interface/napi/mech_manager",
  ]
}

subsystem_name = "distributedhardware"
part_name = "mechbody_controller"

taihe_generated_file_path_mech_manager =
    "$taihe_file_path/out/$subsystem_name/$part_name/mech_manager"
ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_mech_manager}"
  deps = [ ":mech_manager_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.distributedHardware.mechanicManager.ani.cpp",
    "$taihe_generated_file_path/src/ohos.distributedHardware.mechanicManager.abi.c",
  ]
}

generate_static_abc("mech_manager_abc") {
  base_url = "${taihe_generated_file_path_mech_manager}"
  files = [ "${taihe_generated_file_path_mech_manager}/@ohos.distributedHardware.mechanicManager.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/mech_manager_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("mech_manager_etc") {
  source = "$target_out_dir/mech_manager_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":mech_manager_abc" ]
}

taihe_shared_library("mechanic_manager_ani") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  taihe_generated_file_path = "${taihe_generated_file_path_mech_manager}"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  configs = [ ":mech_manager_config" ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ani_mech_manager.cpp",
    "src/ani_mech_manager_client.cpp",
    "src/ani_mech_manager_stub.cpp",
    "src/ohos.distributedHardware.mechanicManager.impl.cpp",
  ]

  cflags = []
  if (mechbody_controller_feature_L1) {
    cflags += [ "-DMECHBODY_CONTROLLER_L1" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  cflags_cc = [
      "-fstack-protector-strong",
  ]
  ldflags = [
    "-fpie",
    "-Wl,-z,relro",
    "-Wl,-z,now",
  ]
  deps = [ ":run_taihe" ]
  external_deps = [
    "access_token:libtokenid_sdk",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]
}

group("mechanic_manager_group_ani") {
  deps = [
    ":mech_manager_etc",
    ":mechanic_manager_ani",
  ]
}
